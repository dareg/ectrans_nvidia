! (C) Copyright 1995- ECMWF.
! (C) Copyright 2022- NVIDIA.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

MODULE TRGTOL_MOD
  CONTAINS
  SUBROUTINE TRGTOL_CUDAAWARE(PREEL_REAL,KF_FS,KF_GP,KVSET,KPTRGP,&
   &PGP,PGPUV,PGP3A,PGP3B,PGP2)

  !**** *TRGTOL * - transposition of grid point data from column
  !                 structure to latitudinal. Reorganize data between
  !                 grid point calculations and direct Fourier Transform

  ! Version using CUDA-aware MPI

  !     Purpose.
  !     --------


  !**   Interface.
  !     ----------
  !        *call* *trgtol(...)

  !        Explicit arguments :
  !        --------------------
  !           PREEL_REAL    -  Latitudinal data ready for direct FFT (output)
  !           PGP    -  Blocked grid point data    (input)

  !        Implicit arguments :
  !        --------------------

  !     Method.
  !     -------
  !        See documentation

  !     Externals.
  !     ----------

  !     Reference.
  !     ----------
  !        ECMWF Research Department documentation of the IFS

  !     Author.
  !     -------
  !        MPP Group *ECMWF*

  !     Modifications.
  !     --------------
  !        Original: 95-10-01
  !        D.Dent  : 97-08-04   Reorganisation to allow
  !                             NPRTRV to differ from NPRGPEW
  !                : 98-06-17   add mailbox control logic (from TRLTOM)
  !        =99-03-29= Mats Hamrud and Deborah Salmond
  !                   JUMP in FFT's changed to 1
  !                   KINDEX introduced and ZCOMBUF not used for same PE
  !         01-11-23  Deborah Salmond and John Hague
  !                    LIMP_NOOLAP Option for non-overlapping message passing
  !                    and buffer packing
  !         01-12-18  Peter Towers
  !                   Improved vector performance of GTOL_PACK,GTOL_UNPACK
  !         03-04-02  G. Radnoti: call barrier always when nproc>1
  !         08-01-01  G.Mozdzynski: cleanup
  !         09-01-02  G.Mozdzynski: use non-blocking recv and send
  !     ------------------------------------------------------------------



  USE PARKIND_ECTRANS ,ONLY : JPIM     ,JPRB ,  JPRBT
  USE YOMHOOK         ,ONLY : LHOOK,   DR_HOOK,  JPHOOK
  USE MPL_MODULE      ,ONLY : MPL_WAIT, MPL_BARRIER
  USE TPM_GEN         ,ONLY : LSYNC_TRANS
  USE EQ_REGIONS_MOD  ,ONLY : MY_REGION_EW, MY_REGION_NS
  USE TPM_DISTR       ,ONLY : D,MYSETV, MYSETW, MTAGLG,NPRCIDS,MYPROC,NPROC,NPRTRW,NPRTRV
  USE PE2SET_MOD      ,ONLY : PE2SET
  USE MPL_DATA_MODULE ,ONLY : MPL_COMM_OML
  USE OML_MOD         ,ONLY : OML_MY_THREAD
  USE MPI
  USE TPM_STATS, ONLY : GSTATS => GSTATS_NVTX

  USE TPM_TRANS       ,ONLY : NPROMA

  IMPLICIT NONE

  REAL(KIND=JPRBT),INTENT(OUT)   :: PREEL_REAL(:)
  INTEGER(KIND=JPIM),INTENT(IN) :: KVSET(:)
  INTEGER(KIND=JPIM),INTENT(IN) :: KF_FS,KF_GP
  INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KPTRGP(:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGPUV(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3A(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3B(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP2(:,:,:)
  REAL(KIND=JPRBT),ALLOCATABLE   :: PREEL_REAL_TMP(:)

  ! LOCAL VARIABLES

  !     LOCAL INTEGER SCALARS
  REAL(KIND=JPRBT),ALLOCATABLE :: ZCOMBUFS(:),ZCOMBUFR(:)

  INTEGER(KIND=JPIM) :: ISENDTOT (NPROC)
  INTEGER(KIND=JPIM) :: IRECVTOT (NPROC)
  INTEGER(KIND=JPIM) :: IREQ     (NPROC*2)
  INTEGER(KIND=JPIM) :: IRECV_TO_PROC(NPROC)
  INTEGER(KIND=JPIM) :: ISEND_TO_PROC(NPROC)

  INTEGER(KIND=JPIM) :: IFIRSTLAT, IGL, IGLL, ILAST,&
               &ILASTLAT, ILEN, JROC, IPOS, ISETA, &
               &ISETB, IRECV, &
               &ISETV, ISEND, JBLK, JFLD, &
               &JGL, JI, JK, JL, ISETW,  IFLD, &
               &II,IBUFLENR,IRECV_COUNTS, IPROC,IFLDS, &
               &ISEND_COUNTS,INS,INR,IR, JKL, PBOUND, IERROR, ILOCAL_LAT
  INTEGER(KIND=JPIM) :: KF, KGL, KI

  INTEGER(KIND=JPIM) :: IOFF, ILAT_STRIP
  INTEGER(KIND=JPIM) :: IRECV_BUFR_TO_OUT(D%NLENGTF),IRECV_BUFR_TO_OUT_OFFSET(NPROC), IRECV_BUFR_TO_OUT_V
  INTEGER(KIND=JPIM) :: ISEND_FIELD_COUNT(NPRTRV),ISEND_FIELD_COUNT_V
  INTEGER(KIND=JPIM) :: ISEND_WSET_SIZE(NPRTRW),ISEND_WSET_SIZE_V
  INTEGER(KIND=JPIM) :: ISEND_WSET_OFFSET(NPRTRW+1), ISEND_WSET_OFFSET_V
  INTEGER(KIND=JPIM), ALLOCATABLE :: ICOMBUFS_OFFSET(:),ICOMBUFR_OFFSET(:)
  INTEGER(KIND=JPIM) :: ICOMBUFS_OFFSET_V, ICOMBUFR_OFFSET_V
  INTEGER(KIND=JPIM) :: IFLDA(KF_GP)

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE_BAR

  INTEGER(JPIM), PARAMETER :: PGP_INDICES_UV = 1
  INTEGER(JPIM), PARAMETER :: PGP_INDICES_GP2 = 2
  INTEGER(JPIM), PARAMETER :: PGP_INDICES_GP3A = 3
  INTEGER(JPIM), PARAMETER :: PGP_INDICES_GP3B = 4
  INTEGER(JPIM), PARAMETER :: PGP_INDICES_END = 5
  INTEGER(JPIM) :: PGP_INDICES(PGP_INDICES_END)

  #ifdef PARKINDTRANS_SINGLE
  #define TRGTOL_DTYPE MPI_REAL
  #else
  #define TRGTOL_DTYPE MPI_DOUBLE_PRECISION
  #endif

ALLOCATE(PREEL_REAL_TMP(KF_FS*D%NLENGTF))
  !     ------------------------------------------------------------------

  !*       0.    Some initializations
  !              --------------------

  IF (LHOOK) CALL DR_HOOK('TRGTOL_CUDAAWARE',0,ZHOOK_HANDLE)

  CALL GSTATS(1805,0)
  IOFF=1
  PGP_INDICES(PGP_INDICES_UV) = IOFF
  IF (PRESENT(PGPUV)) IOFF=IOFF+UBOUND(PGPUV,2)*2
  PGP_INDICES(PGP_INDICES_GP2) = IOFF
  IF (PRESENT(PGP2)) IOFF=IOFF+UBOUND(PGP2,2)
  PGP_INDICES(PGP_INDICES_GP3A) = IOFF
  IF (PRESENT(PGP3A)) IOFF=IOFF+UBOUND(PGP3A,2)*UBOUND(PGP3A,3)
  PGP_INDICES(PGP_INDICES_GP3B) = IOFF
  IF (PRESENT(PGP3B)) IOFF=IOFF+UBOUND(PGP3B,2)*UBOUND(PGP3B,3)
  PGP_INDICES(PGP_INDICES_END) = IOFF

  ! Prepare sender arrays
  ! find number of fields on a certain V-set
  IF(NPRTRV == 1) THEN
    ! This is needed because KVSET(JFLD) == -1 if there is only one V-set
    ISEND_FIELD_COUNT(1) = KF_GP
  ELSE
    ISEND_FIELD_COUNT(:) = 0
    DO JFLD=1,KF_GP
      ISEND_FIELD_COUNT(KVSET(JFLD)) = ISEND_FIELD_COUNT(KVSET(JFLD)) + 1
    ENDDO
  ENDIF
  ! find number of grid-points on a certain W-set that overlap with myself
  ISEND_WSET_SIZE(:) = 0
  DO ILOCAL_LAT=D%NFRSTLAT(MY_REGION_NS),D%NLSTLAT(MY_REGION_NS)
    ILAT_STRIP = ILOCAL_LAT-D%NFRSTLAT(MY_REGION_NS)+D%NPTRFLOFF+1
    ISEND_WSET_SIZE(D%NPROCL(ILOCAL_LAT)) = &
        & ISEND_WSET_SIZE(D%NPROCL(ILOCAL_LAT))+D%NONL(ILAT_STRIP,MY_REGION_EW)
  ENDDO
  ! sum up offsets
  ISEND_WSET_OFFSET(1) = 0
  DO JROC=1,NPRTRW
    ISEND_WSET_OFFSET(JROC+1)=ISEND_WSET_OFFSET(JROC)+ISEND_WSET_SIZE(JROC)
  ENDDO
  DO JROC=1,NPROC
    CALL PE2SET(JROC,ISETA,ISETB,ISETW,ISETV)
    ! total send size is # points per field * # fields
    ISENDTOT(JROC) = ISEND_WSET_SIZE(ISETW)*ISEND_FIELD_COUNT(ISETV)
  ENDDO

  ! Prepare receiver arrays
  IRECV_BUFR_TO_OUT_OFFSET(:) = 0
  DO JROC=1,NPROC
    ! Get new offset to my current KINDEX entry
    IF (JROC > 1 .AND. KF_FS > 0) THEN
      IRECV_BUFR_TO_OUT_OFFSET(JROC) = IRECV_BUFR_TO_OUT_OFFSET(JROC-1)+IRECVTOT(JROC-1)/KF_FS
    ELSEIF (JROC > 1) THEN
      IRECV_BUFR_TO_OUT_OFFSET(JROC) = IRECV_BUFR_TO_OUT_OFFSET(JROC-1)
    ENDIF

    CALL PE2SET(JROC,ISETA,ISETB,ISETW,ISETV)

    ! MAX(Index of first fourier latitude for this W set, first latitude of a senders A set)
    ! i.e. we find the overlap between what we have on sender side (others A set) and the receiver
    ! (me, the W-set). Ideally those conincide, at least mostly.
    IFIRSTLAT = MAX(D%NPTRLS(MYSETW),D%NFRSTLAT(ISETA))
    ! MIN(Index of last fourier latitude for this W set, last latitude of a senders A set)
    ILASTLAT  = MIN(D%NPTRLS(MYSETW)+D%NULTPP(MYSETW)-1,D%NLSTLAT(ISETA))

    IPOS = 0
    DO JGL=IFIRSTLAT,ILASTLAT
      ! get from "actual" latitude to the latitude strip offset
      IGL  = JGL-D%NFRSTLAT(ISETA)+D%NPTRFRSTLAT(ISETA)
      ! get from "actual" latitude to the latitude offset
      IGLL = JGL-D%NPTRLS(MYSETW)+1
      DO JL=1,D%NONL(IGL,ISETB)
        IPOS = IPOS+1
        ! indicates where the data has to be stored
        IRECV_BUFR_TO_OUT(IRECV_BUFR_TO_OUT_OFFSET(JROC)+IPOS) = D%NSTA(IGL,ISETB)+D%NSTAGTF(IGLL)+JL-1
      ENDDO
    ENDDO
    !we always receive the full fourier space
    IRECVTOT(JROC) = IPOS*KF_FS
  ENDDO

  !$ACC DATA COPYIN(IRECV_BUFR_TO_OUT,PGP_INDICES) PRESENT(PREEL_REAL) CREATE(PREEL_REAL_TMP) ASYNC(1)

  CALL GSTATS(1805,1)

  ! Put data on device for copyin
  IF (LSYNC_TRANS) THEN
    !$ACC WAIT(1)
    CALL GSTATS(430,0)
    CALL MPL_BARRIER(CDSTRING='')
    CALL GSTATS(430,1)
  ENDIF
  CALL GSTATS(412,0)
  !$ACC DATA IF(PRESENT(PGP))   COPYIN(PGP) ASYNC(1)
  !$ACC DATA IF(PRESENT(PGPUV)) COPYIN(PGPUV) ASYNC(1)
  !$ACC DATA IF(PRESENT(PGP2))  COPYIN(PGP2) ASYNC(1)
  !$ACC DATA IF(PRESENT(PGP3A)) COPYIN(PGP3A) ASYNC(1)
  !$ACC DATA IF(PRESENT(PGP3B)) COPYIN(PGP3B) ASYNC(1)
  IF (LSYNC_TRANS) THEN
    !$ACC WAIT(1)
    CALL GSTATS(432,0)
    CALL MPL_BARRIER(CDSTRING='')
    CALL GSTATS(432,1)
  ENDIF
  CALL GSTATS(412,1)

  ! TODO We should do the local contribution *WHILE* sending the data...
  ! Copy local contribution
  IF(ISENDTOT(MYPROC) > 0 )THEN
    ! I have to send something to myself...

    ! Input is KF_GP fields. We find the resulting KF_FS fields.
    IFLDS = 0
    DO JFLD=1,KF_GP
      IF(KVSET(JFLD) == MYSETV .OR. KVSET(JFLD) == -1) THEN
        IFLDS = IFLDS+1
        IF(PRESENT(KPTRGP)) THEN
          IFLDA(IFLDS) = KPTRGP(JFLD)
        ELSE
          IFLDA(IFLDS) = JFLD
        ENDIF
      ENDIF
    ENDDO

    !$ACC DATA COPYIN(IFLDA(1:IFLDS)) ASYNC(1)

    ISEND_WSET_OFFSET_V = ISEND_WSET_OFFSET(MYSETW)
    ISEND_WSET_SIZE_V = ISEND_WSET_SIZE(MYSETW)
    IRECV_BUFR_TO_OUT_V = IRECV_BUFR_TO_OUT_OFFSET(MYPROC)
    CALL GSTATS(1601,0)
    IF(PRESENT(PGP)) THEN
      !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE) PRIVATE(JK,JBLK,IFLD,IPOS,IOFF) ASYNC(1)
      DO JL=1,ISEND_WSET_SIZE_V
        DO JFLD=1,KF_FS
          JK = MOD(ISEND_WSET_OFFSET_V+JL-1,NPROMA)+1
          JBLK = (ISEND_WSET_OFFSET_V+JL-1)/NPROMA+1
          IFLD = IFLDA(JFLD)
          IPOS = IRECV_BUFR_TO_OUT_V+JL
          PREEL_REAL_TMP(JFLD+KF_FS*(IRECV_BUFR_TO_OUT(IPOS)-1)) = PGP(JK,IFLD,JBLK)
        ENDDO
      ENDDO
    ELSE
      !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE) PRIVATE(JK,JBLK,IFLD,IPOS,IOFF) ASYNC(1)
      DO JL=1,ISEND_WSET_SIZE_V
        DO JFLD=1,KF_FS
          JK = MOD(ISEND_WSET_OFFSET_V+JL-1,NPROMA)+1
          JBLK = (ISEND_WSET_OFFSET_V+JL-1)/NPROMA+1
          IFLD = IFLDA(JFLD)
          IPOS = IRECV_BUFR_TO_OUT(IRECV_BUFR_TO_OUT_V+JL)-1
          IF(IFLD < PGP_INDICES(PGP_INDICES_UV+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_UV)
            PBOUND=UBOUND(PGPUV,2)
            PREEL_REAL_TMP(JFLD+KF_FS*IPOS) = PGPUV(JK,MOD(IOFF,PBOUND)+1,IOFF/PBOUND+1,JBLK)
          ELSEIF(IFLD < PGP_INDICES(PGP_INDICES_GP2+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_GP2)
            PREEL_REAL_TMP(JFLD+KF_FS*IPOS) = PGP2(JK,IOFF+1,JBLK)
          ELSEIF(IFLD < PGP_INDICES(PGP_INDICES_GP3A+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_GP3A)
            PBOUND=UBOUND(PGP3A,2)
            PREEL_REAL_TMP(JFLD+KF_FS*IPOS) = PGP3A(JK,MOD(IOFF,PBOUND)+1,IOFF/PBOUND+1,JBLK)
          ELSEIF(IFLD < PGP_INDICES(PGP_INDICES_GP3B+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_GP3B)
            PBOUND=UBOUND(PGP3B,2)
            PREEL_REAL_TMP(JFLD+KF_FS*IPOS) = PGP3B(JK,MOD(IOFF,PBOUND)+1,IOFF/PBOUND+1,JBLK)
          ENDIF
        ENDDO
      ENDDO
    ENDIF
    CALL GSTATS(1601,1)

    !$ACC END DATA

  ENDIF

  ! Figure out processes that send or recv something
  ISEND_COUNTS   = 0
  IRECV_COUNTS   = 0
  DO JROC=1,NPROC
    IF( JROC /= MYPROC) THEN
      IF(IRECVTOT(JROC) > 0) THEN
        ! I have to recv something, so let me store that
        IRECV_COUNTS = IRECV_COUNTS + 1
        IRECV_TO_PROC(IRECV_COUNTS)=JROC
      ENDIF
      IF(ISENDTOT(JROC) > 0) THEN
        ! I have to send something, so let me store that
        ISEND_COUNTS = ISEND_COUNTS+1
        ISEND_TO_PROC(ISEND_COUNTS)=JROC
      ENDIF
    ENDIF
  ENDDO

  ALLOCATE(ICOMBUFS_OFFSET(ISEND_COUNTS+1))
  ICOMBUFS_OFFSET(1) = 0
  DO JROC=1,ISEND_COUNTS
    ICOMBUFS_OFFSET(JROC+1) = ICOMBUFS_OFFSET(JROC) + ISENDTOT(ISEND_TO_PROC(JROC))
  ENDDO
  ALLOCATE(ICOMBUFR_OFFSET(IRECV_COUNTS+1))
  ICOMBUFR_OFFSET(1) = 0
  DO JROC=1,IRECV_COUNTS
    ICOMBUFR_OFFSET(JROC+1) = ICOMBUFR_OFFSET(JROC) + IRECVTOT(IRECV_TO_PROC(JROC))
  ENDDO

  ! Do this with "enter data" syntax because we are in the PGP data clause
  IF (ISEND_COUNTS > 0) ALLOCATE(ZCOMBUFS(ICOMBUFS_OFFSET(ISEND_COUNTS+1)))
  !$ACC ENTER DATA IF(ISEND_COUNTS > 0) CREATE(ZCOMBUFS) ASYNC(1)

  !....Pack loop.........................................................
  !$ACC DATA IF(ISEND_COUNTS > 0) PRESENT(ZCOMBUFS) ASYNC(1)

  CALL GSTATS(1602,0)
  DO INS=1,ISEND_COUNTS
    ISEND=ISEND_TO_PROC(INS)
    CALL PE2SET(ISEND,ISETA,ISETB,ISETW,ISETV)

    ISEND_FIELD_COUNT_V = ISEND_FIELD_COUNT(ISETV)
    ICOMBUFS_OFFSET_V = ICOMBUFS_OFFSET(INS)

    IFLDS = 0
    DO JFLD=1,KF_GP
      IF(KVSET(JFLD) == ISETV .OR. KVSET(JFLD) == -1 ) THEN
        IFLDS = IFLDS+1
        IF(PRESENT(KPTRGP)) THEN
          IFLDA(IFLDS)=KPTRGP(JFLD)
        ELSE
          IFLDA(IFLDS)=JFLD
        ENDIF
      ENDIF
    ENDDO

    !$ACC DATA COPYIN(IFLDA(1:ISEND_FIELD_COUNT_V)) ASYNC(1)

    ISEND_WSET_OFFSET_V = ISEND_WSET_OFFSET(ISETW)
    ISEND_WSET_SIZE_V = ISEND_WSET_SIZE(ISETW)
    IF(PRESENT(PGP)) THEN
      !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE) PRIVATE(JK,JBLK,IFLD,JI) ASYNC(1)
      DO JL=1,ISEND_WSET_SIZE_V
        DO JFLD=1,ISEND_FIELD_COUNT_V
          JK = MOD(ISEND_WSET_OFFSET_V+JL-1,NPROMA)+1
          JBLK = (ISEND_WSET_OFFSET_V+JL-1)/NPROMA+1
          IFLD = IFLDA(JFLD)
          JI = (JFLD-1)*ISEND_WSET_SIZE_V+JL
          ZCOMBUFS(ICOMBUFS_OFFSET_V+JI) = PGP(JK,IFLD,JBLK)
        ENDDO
      ENDDO
    ELSE
      !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE) PRIVATE(JK,JBLK,IFLD,JI,IOFF) ASYNC(1)
      DO JL=1,ISEND_WSET_SIZE_V
        DO JFLD=1,ISEND_FIELD_COUNT_V
          JK = MOD(ISEND_WSET_OFFSET_V+JL-1,NPROMA)+1
          JBLK = (ISEND_WSET_OFFSET_V+JL-1)/NPROMA+1
          IFLD = IFLDA(JFLD)
          JI = ICOMBUFS_OFFSET_V+(JFLD-1)*ISEND_WSET_SIZE_V+JL
          IF(IFLD < PGP_INDICES(PGP_INDICES_UV+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_UV)
            PBOUND=UBOUND(PGPUV,2)
            ! TODO we could certainly reshape PGPXX arrays and we would simplify this
            ZCOMBUFS(JI) = PGPUV(JK,MOD(IOFF,PBOUND)+1,IOFF/PBOUND+1,JBLK)
          ELSEIF(IFLD < PGP_INDICES(PGP_INDICES_GP2+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_GP2)
            ZCOMBUFS(JI)  = PGP2(JK,IOFF+1,JBLK)
          ELSEIF(IFLD < PGP_INDICES(PGP_INDICES_GP3A+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_GP3A)
            PBOUND=UBOUND(PGP3A,2)
            ZCOMBUFS(JI) = PGP3A(JK,MOD(IOFF,PBOUND)+1,IOFF/PBOUND+1,JBLK)
          ELSEIF(IFLD < PGP_INDICES(PGP_INDICES_GP3B+1)) THEN
            IOFF=IFLD-PGP_INDICES(PGP_INDICES_GP3B)
            PBOUND=UBOUND(PGP3B,2)
            ZCOMBUFS(JI)= PGP3B(JK,MOD(IOFF,PBOUND)+1,IOFF/PBOUND+1,JBLK)
          ENDIF
       ENDDO
      ENDDO
    ENDIF
    !$ACC END DATA
  ENDDO
  !$ACC END DATA !ZCOMBUFS (present)
  !$ACC END DATA !PGP3B
  !$ACC END DATA !PGP3A
  !$ACC END DATA !PGP2
  !$ACC END DATA !PGPUV
  !$ACC END DATA !PGP

  IF (IRECV_COUNTS > 0) ALLOCATE(ZCOMBUFR(ICOMBUFR_OFFSET(IRECV_COUNTS+1)))
  !$ACC DATA IF(IRECV_COUNTS > 0) CREATE(ZCOMBUFR) ASYNC(1)

  !$ACC WAIT(1)

  CALL GSTATS(1602,1)

  IF (LSYNC_TRANS) THEN
    CALL GSTATS(430,0)
    CALL MPL_BARRIER(CDSTRING='')
    CALL GSTATS(430,1)
  ENDIF
  CALL GSTATS(411,0)

  IR=0

  !$ACC HOST_DATA USE_DEVICE(ZCOMBUFR,ZCOMBUFS)
  !  Receive loop.........................................................
  DO INR=1,IRECV_COUNTS
    IR=IR+1
    IPROC=IRECV_TO_PROC(INR)
    CALL MPI_IRECV(ZCOMBUFR(ICOMBUFR_OFFSET(INR)+1:ICOMBUFR_OFFSET(INR+1)),IRECVTOT(IPROC), &
      & TRGTOL_DTYPE,NPRCIDS(IPROC)-1,MTAGLG,MPL_COMM_OML(OML_MY_THREAD()),IREQ(IR),IERROR)
  ENDDO

  !....Send loop.........................................................
  DO INS=1,ISEND_COUNTS
    IR=IR+1
    ISEND=ISEND_TO_PROC(INS)
    CALL MPI_ISEND(ZCOMBUFS(ICOMBUFS_OFFSET(INS)+1:ICOMBUFS_OFFSET(INS+1)),ISENDTOT(ISEND), &
     & TRGTOL_DTYPE,NPRCIDS(ISEND)-1,MTAGLG,MPL_COMM_OML(OML_MY_THREAD()),IREQ(IR),IERROR)
  ENDDO
  !$ACC END HOST_DATA

  IF(IR > 0) THEN
    CALL MPL_WAIT(KREQUEST=IREQ(1:IR), &
      & CDSTRING='TRGTOL_CUDAAWARE: WAIT FOR SENDS AND RECEIVES')
  ENDIF
  IF (LSYNC_TRANS) THEN
    CALL GSTATS(431,0)
    CALL MPL_BARRIER(CDSTRING='')
    CALL GSTATS(431,1)
  ENDIF
  CALL GSTATS(411,1)

  !$ACC EXIT DATA IF(ISEND_COUNTS > 0) DELETE(ZCOMBUFS)
  IF (ISEND_COUNTS > 0) DEALLOCATE(ZCOMBUFS)

  !  Unpack loop.........................................................

  CALL GSTATS(1603,0)


  DO INR=1,IRECV_COUNTS
    IPROC=IRECV_TO_PROC(INR)
    ILEN = IRECVTOT(IPROC)/KF_FS
    IRECV_BUFR_TO_OUT_V = IRECV_BUFR_TO_OUT_OFFSET(IPROC)
    ICOMBUFR_OFFSET_V = ICOMBUFR_OFFSET(INR)
    !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE) PRIVATE(II) ASYNC(1)
    DO JFLD=1,KF_FS
      DO JL=1,ILEN
        II = IRECV_BUFR_TO_OUT(IRECV_BUFR_TO_OUT_V+JL)-1
        PREEL_REAL_TMP(JFLD+KF_FS*II) = ZCOMBUFR(ICOMBUFR_OFFSET_V+JL+(JFLD-1)*ILEN)
      ENDDO
    ENDDO
  ENDDO

  !$ACC WAIT(1)
  !$ACC KERNELS
  PREEL_REAL(:) = -1
  !$ACC END KERNELS
  !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE)
  DO KGL=1,D%NDGL_FS
    DO KF=1,KF_FS
      !$ACC LOOP SEQ
      DO KI=1,D%NSTAGTF(KGL+1)-D%NSTAGTF(KGL)
        PREEL_REAL(KF_FS*D%NSTAGTF(KGL)+(KF-1)*(D%NSTAGTF(KGL+1)-D%NSTAGTF(KGL))+KI) = &
            & PREEL_REAL_TMP(KF_FS*D%NSTAGTF(KGL)+(KI-1)*KF_FS+KF)
      ENDDO
    ENDDO
  ENDDO
  CALL GSTATS(1603,1)

  !$ACC END DATA ! ZCOMBUFR
  !$ACC END DATA ! IRECV_BUFR_TO_OUT,PGPINDICES

  IF (IRECV_COUNTS > 0) DEALLOCATE(ZCOMBUFR)

  IF (LHOOK) CALL DR_HOOK('TRGTOL_CUDAAWARE',1,ZHOOK_HANDLE)

  END SUBROUTINE TRGTOL_CUDAAWARE

  SUBROUTINE TRGTOL(PREEL_REAL,KF_FS,KF_GP,KF_SCALARS_G,KVSET,KPTRGP,&
   &PGP,PGPUV,PGP3A,PGP3B,PGP2)
  
  !**** *TRGTOL * - transposition of grid point data from column
  !                 structure to latitudinal. Reorganize data between
  !                 grid point calculations and direct Fourier Transform
  
  !     Purpose.
  !     --------
  
  
  !**   Interface.
  !     ----------
  !        *call* *trgtol(...)
  
  !        Explicit arguments :
  !        --------------------
  !           PREEL_REAL    -  Latitudinal data ready for direct FFT (output)
  !           PGP    -  Blocked grid point data    (input)
  
  !        Implicit arguments :
  !        --------------------
  
  !     Method.
  !     -------
  !        See documentation
  
  !     Externals.
  !     ----------
  
  !     Reference.
  !     ----------
  !        ECMWF Research Department documentation of the IFS
  
  !     Author.
  !     -------
  !        MPP Group *ECMWF*
  
  !     Modifications.
  !     --------------
  !        Original: 95-10-01
  !        D.Dent  : 97-08-04   Reorganisation to allow
  !                             NPRTRV to differ from NPRGPEW
  !                : 98-06-17   add mailbox control logic (from TRLTOM)
  !        =99-03-29= Mats Hamrud and Deborah Salmond
  !                   JUMP in FFT's changed to 1
  !                   KINDEX introduced and ZCOMBUF not used for same PE
  !         01-11-23  Deborah Salmond and John Hague
  !                    LIMP_NOOLAP Option for non-overlapping message passing
  !                    and buffer packing
  !         01-12-18  Peter Towers
  !                   Improved vector performance of GTOL_PACK,GTOL_UNPACK
  !         03-04-02  G. Radnoti: call barrier always when nproc>1
  !         08-01-01  G.Mozdzynski: cleanup
  !         09-01-02  G.Mozdzynski: use non-blocking recv and send
  !     ------------------------------------------------------------------
  
  
  
  USE PARKIND_ECTRANS  ,ONLY : JPIM     ,JPRB,  JPRBT
  USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK, JPHOOK
  
  USE MPL_MODULE  ,ONLY : MPL_RECV, MPL_SEND, MPL_WAIT, JP_NON_BLOCKING_STANDARD
  
  USE TPM_GEN         ,ONLY : NOUT
  USE TPM_DISTR       ,ONLY : D, NPRCIDS, NPRTRNS, MTAGGL,  &
       &                      MYSETV, MYSETW, MYPROC, NPROC
  USE TPM_TRANS       ,ONLY : LDIVGP, LGPNORM, LSCDERS, LUVDER, LVORGP, NGPBLKS
  
  USE INIGPTR_MOD     ,ONLY : INIGPTR
  USE PE2SET_MOD      ,ONLY : PE2SET
  !USE MYSENDSET_MOD
  !USE MYRECVSET_MOD
  USE ABORT_TRANS_MOD ,ONLY : ABORT_TRANS
  !
  USE MPI
  
  IMPLICIT NONE
  
  REAL(KIND=JPRBT),INTENT(OUT)   :: PREEL_REAL(:)
  INTEGER(KIND=JPIM),INTENT(IN) :: KVSET(:)
  INTEGER(KIND=JPIM),INTENT(IN) :: KF_FS,KF_GP
  INTEGER(KIND=JPIM),INTENT(IN) :: KF_SCALARS_G
  INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KPTRGP(:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGPUV(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3A(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3B(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP2(:,:,:)
  
  REAL(KIND=JPRBT),ALLOCATABLE :: ZCOMBUFS(:,:),ZCOMBUFR(:,:)
  REAL(KIND=JPRBT),ALLOCATABLE   :: PREEL_REAL_TMP(:)
  REAL(KIND=JPRBT) :: ZDUM(2)
  
  INTEGER(KIND=JPIM) :: ISENT    (NPROC)
  INTEGER(KIND=JPIM) :: IRCVD    (NPROC)
  INTEGER(KIND=JPIM) :: ISENDTOT (NPROC)
  INTEGER(KIND=JPIM) :: IRECVTOT (NPROC)
  INTEGER(KIND=JPIM) :: IREQ     (NPROC*2)
  INTEGER(KIND=JPIM) :: JRECV    (NPROC)
  INTEGER(KIND=JPIM) :: JSEND    (NPROC)
  
  !     LOCAL INTEGER SCALARS
  INTEGER(KIND=JPIM) :: IFIRST, IFIRSTLAT, IGL, IGLL, ILAST,&
               &ILASTLAT, ILEN, JROC, IPOS, ISETA, &
               &ISETB, IRECV, IRECVSET, &
               &ISETV, ISEND, ISENDSET, ITAG, J, JBLK, JFLD, &
               &JGL, JK, JL, JLOOP, ISETW,  IFLD, &
               &II,INDOFFX,IBUFLENS,IBUFLENR,INRECV, IPROC,IFLDS, &
               &INSEND,INS,INR,IR, iunit
  
  !     LOCAL LOGICAL SCALARS
  LOGICAL   :: LLPGPUV,LLPGP3A,LLPGP3B,LLPGP2,LLPGPONLY
  LOGICAL   :: LLUV(KF_GP),LLGP2(KF_GP),LLGP3A(KF_GP),LLGP3B(KF_GP)
  LOGICAL :: LLDONE, LLINDER
  INTEGER(KIND=JPIM) :: IUVLEVS(KF_GP),IUVPARS(KF_GP),IGP2PARS(KF_GP)
  INTEGER(KIND=JPIM) :: IGP3APARS(KF_GP),IGP3ALEVS(KF_GP),IGP3BPARS(KF_GP),IGP3BLEVS(KF_GP)
  INTEGER(KIND=JPIM) :: IUVPAR,IUVLEV,IGP2PAR,IGP3ALEV,IGP3APAR,IGP3BLEV,IGP3BPAR,IPAROFF
  INTEGER(KIND=JPIM) :: IOFF,IOFF1,IOFFNS,IOFFEW,J1,J2
  INTEGER(KIND=JPIM) :: KINDEX(D%NLENGTF),INDOFF(NPROC),IFLDOFF(MAX(KF_GP,KF_FS))
  INTEGER(KIND=JPIM) :: ISEND_FLD_TOTAL(NPROC),ISEND_FLD_START(NPROC),ISEND_FLD_END
  INTEGER(KIND=JPIM) :: IRECV_FLD_START,IRECV_FLD_END
  INTEGER(KIND=JPIM) :: INUMFLDS
  INTEGER(KIND=JPIM) :: IGPTRSEND(2,NGPBLKS,NPRTRNS)
  INTEGER(KIND=JPIM) :: IGPTRRECV(NPRTRNS)
  INTEGER(KIND=JPIM) :: IGPTROFF(NGPBLKS)
  !     INTEGER FUNCTIONS
  INTEGER(KIND=JPIM) :: IRECVCOUNT,ISENDCOUNT
  INTEGER(KIND=JPIM) :: IJPOS(NGPBLKS),IFLDA(KF_GP),JJ,JI,IFLDT
  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE_BAR
  
  INTEGER(KIND=JPIM) :: IERROR, irank
  INTEGER(KIND=JPIM) :: KF, KGL, KI
  
  REAL(KIND=JPRBT) :: TIMEF, tc
  
  !     ------------------------------------------------------------------
  
  !*       0.    Some initializations
  !              --------------------
  
  IF (LHOOK) CALL DR_HOOK('TRGTOL',0,ZHOOK_HANDLE)
  
  iunit=300+myproc

  CALL GSTATS(1805,0)
  
  LLINDER = .FALSE.
  LLPGPUV = .FALSE.
  LLPGP3A = .FALSE.
  LLPGP3B = .FALSE.
  LLPGP2  = .FALSE.
  LLPGPONLY = .FALSE.
  IF(PRESENT(KPTRGP))  LLINDER = .TRUE.
  IF(PRESENT(PGP))     LLPGPONLY = .TRUE.
  IF(PRESENT(PGPUV))   LLPGPUV = .TRUE.
  IF(PRESENT(PGP3A))   LLPGP3A = .TRUE.
  IF(PRESENT(PGP3B))   LLPGP3B = .TRUE.
  IF(PRESENT(PGP2))    LLPGP2 = .TRUE.
  IUVPAR=0
  IUVLEV=0
  IOFF1=0
  IOFFNS=KF_SCALARS_G
  IOFFEW=2*KF_SCALARS_G
  LLUV(:) = .FALSE.
  IUVPARS(:) = -99
  IUVLEVS(:) = -99
  IF (LLPGPUV) THEN
    IOFF=0
    IUVLEV=UBOUND(PGPUV,2)
    IF(LVORGP) THEN
      IUVPAR=IUVPAR+1
      DO J=1,IUVLEV
        IUVLEVS(IOFF+J)=J
        IUVPARS(IOFF+J)=IUVPAR
        LLUV(IOFF+J)=.TRUE.
      ENDDO
      IOFF=IOFF+IUVLEV
    ENDIF
    IF(LDIVGP) THEN
      IUVPAR=IUVPAR+1
      DO J=1,IUVLEV
        IUVLEVS(IOFF+J)=J
        IUVPARS(IOFF+J)=IUVPAR
        LLUV(IOFF+J)=.TRUE.
      ENDDO
      IOFF=IOFF+IUVLEV
    ENDIF
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR+1
      IUVLEVS(IOFF+J+IUVLEV)=J
      IUVPARS(IOFF+J+IUVLEV)=IUVPAR+2
    ENDDO
    IUVPAR=IUVPAR+2
    LLUV(IOFF+1:IOFF+2*IUVLEV)=.TRUE.
    IOFF=IOFF+2*IUVLEV
    IOFF1=IOFF
    IOFFNS=IOFFNS+IOFF
    IOFFEW=IOFFEW+IOFF
    IOFF=IUVPAR*IUVLEV+KF_SCALARS_G
    IF(LUVDER) THEN
      IF(LSCDERS) IOFF=IOFF+KF_SCALARS_G
      DO J=1,IUVLEV
        IUVLEVS(IOFF+J)=J
        IUVPARS(IOFF+J)=IUVPAR+1
        LLUV(IOFF+J)=.TRUE.
        IUVLEVS(IOFF+J+IUVLEV)=J
        IUVPARS(IOFF+J+IUVLEV)=IUVPAR+2
        LLUV(IOFF+J+IUVLEV)=.TRUE.
      ENDDO
      IUVPAR=IUVPAR+2
      IOFF=IOFF+2*IUVLEV
      IOFFEW=IOFFEW+2*IUVLEV
    ENDIF
  ENDIF
  LLGP2(:)=.FALSE.
  IF(LLPGP2) THEN
    IOFF=IOFF1
    IGP2PAR=UBOUND(PGP2,2)
    IF(LSCDERS) IGP2PAR=IGP2PAR/3
    DO J=1,IGP2PAR
      LLGP2(J+IOFF) = .TRUE.
      IGP2PARS(J+IOFF)=J
    ENDDO
    IOFF1=IOFF1+IGP2PAR
    IF(LSCDERS) THEN
      IOFF=IOFFNS
      DO J=1,IGP2PAR
        LLGP2(J+IOFF) = .TRUE.
        IGP2PARS(J+IOFF)=J+IGP2PAR
      ENDDO
      IOFFNS=IOFF+IGP2PAR
      IOFF=IOFFEW
      DO J=1,IGP2PAR
        LLGP2(J+IOFF) = .TRUE.
        IGP2PARS(J+IOFF)=J+2*IGP2PAR
      ENDDO
      IOFFEW=IOFF+IGP2PAR
    ENDIF
  ENDIF
  LLGP3A(:) = .FALSE.
  IF(LLPGP3A) THEN
    IGP3ALEV=UBOUND(PGP3A,2)
    IGP3APAR=UBOUND(PGP3A,3)
    IF(LSCDERS) IGP3APAR=IGP3APAR/3
    IOFF=IOFF1
    DO J1=1,IGP3APAR
      DO J2=1,IGP3ALEV
        LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
        IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1
        IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
      ENDDO
    ENDDO
    IPAROFF=IGP3APAR
    IOFF1=IOFF1+IGP3APAR*IGP3ALEV
    IF(LSCDERS) THEN
      IOFF=IOFFNS
      DO J1=1,IGP3APAR
        DO J2=1,IGP3ALEV
          LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
          IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1+IPAROFF
          IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
        ENDDO
      ENDDO
      IPAROFF=IPAROFF+IGP3APAR
      IOFFNS=IOFFNS+IGP3APAR*IGP3ALEV
      IOFF=IOFFEW
      DO J1=1,IGP3APAR
        DO J2=1,IGP3ALEV
          LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
          IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1+IPAROFF
          IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
        ENDDO
      ENDDO
      IOFFEW=IOFFEW+IGP3APAR*IGP3ALEV
    ENDIF
  ENDIF
  LLGP3B(:) = .FALSE.
  IF(LLPGP3B) THEN
    IGP3BLEV=UBOUND(PGP3B,2)
    IGP3BPAR=UBOUND(PGP3B,3)
    IF(LSCDERS) IGP3BPAR=IGP3BPAR/3
    IOFF=IOFF1
    DO J1=1,IGP3BPAR
      DO J2=1,IGP3BLEV
        LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
        IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1
        IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
      ENDDO
    ENDDO
    IPAROFF=IGP3BPAR
    IOFF1=IOFF1+IGP3BPAR*IGP3BLEV
    IF(LSCDERS) THEN
      IOFF=IOFFNS
      DO J1=1,IGP3BPAR
        DO J2=1,IGP3BLEV
          LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
          IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1+IPAROFF
          IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
        ENDDO
      ENDDO
      IPAROFF=IPAROFF+IGP3BPAR
      IOFFNS=IOFFNS+IGP3BPAR*IGP3BLEV
      IOFF=IOFFEW
      DO J1=1,IGP3BPAR
        DO J2=1,IGP3BLEV
          LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
          IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1+IPAROFF
          IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
        ENDDO
      ENDDO
      IOFFEW=IOFFEW+IGP3BPAR*IGP3BLEV
    ENDIF
  ENDIF
  
  
  CALL INIGPTR(IGPTRSEND,IGPTRRECV)
  LLDONE = .FALSE.
  
  ITAG = MTAGGL
  
  INDOFFX  = 0
  IBUFLENS = 0
  IBUFLENR = 0
  INRECV   = 0
  INSEND   = 0
  
  DO JROC=1,NPROC
  
    CALL PE2SET(JROC,ISETA,ISETB,ISETW,ISETV)
    IRECVSET = ISETA
    ISEND = JROC
    ISENDSET = ISETV
    ISENT(JROC) = 0
    IRCVD(JROC) = 0
  
  !             count up expected number of fields
    IPOS = 0
    DO JFLD=1,KF_GP
      IF(KVSET(JFLD) == ISENDSET .OR. KVSET(JFLD) == -1) IPOS = IPOS+1
    ENDDO
    ISEND_FLD_TOTAL(JROC) = IPOS
    ISENDTOT(JROC) = IGPTRRECV(ISETW)*IPOS
  
    IF( JROC /= MYPROC) THEN
      IBUFLENS = MAX(IBUFLENS,ISENDTOT(JROC))
      IF(ISENDTOT(JROC) > 0) THEN
        INSEND = INSEND+1
        JSEND(INSEND)=JROC
      ENDIF
    ENDIF
  
    IFIRSTLAT = MAX(D%NPTRLS(MYSETW),D%NFRSTLAT(IRECVSET))
    ILASTLAT  = MIN(D%NPTRLS(MYSETW)+D%NULTPP(MYSETW)-1,D%NLSTLAT(IRECVSET))
  
    IPOS = 0
    DO JGL=IFIRSTLAT,ILASTLAT
      IGL  = D%NPTRFRSTLAT(IRECVSET)+JGL-D%NFRSTLAT(IRECVSET)
      IPOS = IPOS+D%NONL(IGL,ISETB)
    ENDDO
  
    IRECVTOT(JROC) = IPOS*KF_FS
  
    IF(IRECVTOT(JROC) > 0 .AND. MYPROC /= JROC) THEN
      INRECV = INRECV + 1
      JRECV(INRECV)=JROC
    ENDIF
  
    IF( JROC /= MYPROC) IBUFLENR = MAX(IBUFLENR,IRECVTOT(JROC))
  
    IF(IPOS > 0) THEN
      INDOFF(JROC) = INDOFFX
      INDOFFX = INDOFFX+IPOS
      IPOS = 0
      DO JGL=IFIRSTLAT,ILASTLAT
        IGL  = D%NPTRFRSTLAT(IRECVSET)+JGL-D%NFRSTLAT(IRECVSET)
        IGLL = JGL-D%NPTRLS(MYSETW)+1
        DO JL=D%NSTA(IGL,ISETB)+D%NSTAGTF(IGLL),&
         &D%NSTA(IGL,ISETB)+D%NSTAGTF(IGLL)+D%NONL(IGL,ISETB)-1
          IPOS = IPOS+1
          KINDEX(IPOS+INDOFF(JROC)) = JL
        ENDDO
      ENDDO
    ENDIF
  
  ENDDO
  
  ISENDCOUNT=0
  IRECVCOUNT=0
  DO J=1,NPROC
    ISENDCOUNT=MAX(ISENDCOUNT,ISENDTOT(J))
    IRECVCOUNT=MAX(IRECVCOUNT,IRECVTOT(J))
  ENDDO
  IF (IBUFLENS > 0) ALLOCATE(ZCOMBUFS(-1:ISENDCOUNT,INSEND))
  IF (IBUFLENR > 0) ALLOCATE(ZCOMBUFR(-1:IRECVCOUNT,INRECV))

  CALL GSTATS(1805,1)
 
  ! Send loop.............................................................
  
  ! Copy local contribution
  
  IF(ISENDTOT(MYPROC) > 0 )THEN
    IFLDS = 0
    DO JFLD=1,KF_GP
      IF(KVSET(JFLD) == MYSETV .OR. KVSET(JFLD) == -1) THEN
        IFLDS = IFLDS+1
        IF(LLINDER) THEN
          IFLDOFF(IFLDS) = KPTRGP(JFLD)
        ELSE
          IFLDOFF(IFLDS) = JFLD
        ENDIF
      ENDIF
    ENDDO
  
    IPOS=0
    DO JBLK=1,NGPBLKS
      IGPTROFF(JBLK)=IPOS
      IFIRST = IGPTRSEND(1,JBLK,MYSETW)
      IF(IFIRST > 0) THEN
        ILAST = IGPTRSEND(2,JBLK,MYSETW)
        IPOS=IPOS+ILAST-IFIRST+1
      ENDIF
    ENDDO

    CALL GSTATS(1601,0)
  #ifdef NECSX
  !$OMP PARALLEL DO SCHEDULE(DYNAMIC) PRIVATE(JFLD,JBLK,JK,IFLD,IPOS,IFIRST,ILAST)
  #else
  !$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JFLD,JBLK,JK,IFLD,IPOS,IFIRST,ILAST)
  #endif
  DO JBLK=1,NGPBLKS
      IFIRST = IGPTRSEND(1,JBLK,MYSETW)
      IF(IFIRST > 0) THEN
        ILAST = IGPTRSEND(2,JBLK,MYSETW)
        IF(LLPGPONLY) THEN
          DO JK=IFIRST,ILAST
            IPOS = KINDEX(INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1)-1
            DO JFLD=1,IFLDS
              IFLD = IFLDOFF(JFLD)
              PREEL_REAL(JFLD+KF_FS*IPOS) = PGP(JK,IFLD,JBLK)
            ENDDO
          ENDDO
        ELSE
          DO JFLD=1,IFLDS
            IFLD = IFLDOFF(JFLD)
            IF(LLUV(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = KINDEX(INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1)-1
                PREEL_REAL(JFLD+KF_FS*IPOS) = PGPUV(JK,IUVLEVS(IFLD),IUVPARS(IFLD),JBLK)
              ENDDO
            ELSEIF(LLGP2(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = KINDEX(INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1)-1
                PREEL_REAL(JFLD+KF_FS*IPOS) = PGP2(JK,IGP2PARS(IFLD),JBLK)
              ENDDO
            ELSEIF(LLGP3A(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = KINDEX(INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1)-1
                PREEL_REAL(JFLD+KF_FS*IPOS) = PGP3A(JK,IGP3ALEVS(IFLD),IGP3APARS(IFLD),JBLK)
              ENDDO
            ELSEIF(LLGP3B(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = KINDEX(INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1)-1
                PREEL_REAL(JFLD+KF_FS*IPOS) = PGP3B(JK,IGP3BLEVS(IFLD),IGP3BPARS(IFLD),JBLK)
              ENDDO
            ELSE
              CALL ABORT_TRANS('TRLTOG_MOD: ERROR')
            ENDIF
          ENDDO
        ENDIF
      ENDIF
    ENDDO
    !$OMP END PARALLEL DO
    CALL GSTATS(1601,1)
  
  ENDIF

  #ifdef COMVERBOSE
    call MPI_BARRIER(MPI_COMM_WORLD,IERROR)
    Tc=TIMEF()
  #endif
  !....Pack loop.........................................................
  
  ISEND_FLD_START=1
  CALL GSTATS(1602,0)
  !$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(IFLDT,JBLK,IFIRST,ILAST,JK,JJ,JI,&
  !$OMP& INS,ISEND,ISETA,ISETB,ISETW,ISETV,ISENDSET,ISEND_FLD_END,IFLD,IPOS,&
  !$OMP& IFLDA,JFLD,IJPOS)
    DO INS=1,INSEND
      ISEND=JSEND(INS)
      CALL PE2SET(ISEND,ISETA,ISETB,ISETW,ISETV)
      ISENDSET = ISETV
      ISEND_FLD_END = ISEND_FLD_TOTAL(ISEND)
      IFLD = 0
      IPOS = 0
      DO JFLD=1,KF_GP
        IF(KVSET(JFLD) == ISENDSET .OR. KVSET(JFLD) == -1 ) THEN
          IFLD = IFLD+1
          IFLDA(IFLD)=JFLD
        ENDIF
      ENDDO
  
      DO JBLK=1,NGPBLKS
        IFIRST = IGPTRSEND(1,JBLK,ISETW)
        IF(IFIRST > 0) THEN
          ILAST = IGPTRSEND(2,JBLK,ISETW)
          IJPOS(JBLK)=IPOS
          IPOS=IPOS+(ILAST-IFIRST+1)
        ENDIF
      ENDDO
  
  
      DO JJ=ISEND_FLD_START(ISEND),ISEND_FLD_END
        IFLDT=IFLDA(JJ)
        DO JBLK=1,NGPBLKS
          IFIRST = IGPTRSEND(1,JBLK,ISETW)
          IF(IFIRST > 0) THEN
            ILAST = IGPTRSEND(2,JBLK,ISETW)
            IF(LLINDER) THEN
              DO JK=IFIRST,ILAST
                JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
                ZCOMBUFS(JI,INS) = PGP(JK,KPTRGP(IFLDT),JBLK)
              ENDDO
            ELSE
              IF(LLPGPONLY) THEN
                DO JK=IFIRST,ILAST
                  JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
                  ZCOMBUFS(JI,INS) = PGP(JK,IFLDT,JBLK)
                ENDDO
              ELSEIF(LLUV(IFLDT)) THEN
                DO JK=IFIRST,ILAST
                  JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
                  ZCOMBUFS(JI,INS) = PGPUV(JK,IUVLEVS(IFLDT),IUVPARS(IFLDT),JBLK)
                ENDDO
              ELSEIF(LLGP2(IFLDT)) THEN
                DO JK=IFIRST,ILAST
                  JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
                  ZCOMBUFS(JI,INS) = PGP2(JK,IGP2PARS(IFLDT),JBLK)
                ENDDO
              ELSEIF(LLGP3A(IFLDT)) THEN
                DO JK=IFIRST,ILAST
                  JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
                  ZCOMBUFS(JI,INS) = PGP3A(JK,IGP3ALEVS(IFLDT),IGP3APARS(IFLDT),JBLK)
                ENDDO
              ELSEIF(LLGP3B(IFLDT)) THEN
                DO JK=IFIRST,ILAST
                  JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
                  ZCOMBUFS(JI,INS) = PGP3B(JK,IGP3BLEVS(IFLDT),IGP3BPARS(IFLDT),JBLK)
                ENDDO
              ENDIF
            ENDIF
          ENDIF
        ENDDO
      ENDDO
  
      IPOS=(ISEND_FLD_END-ISEND_FLD_START(ISEND)+1)*IPOS
      ZCOMBUFS(-1,INS) = 1
      ZCOMBUFS(0,INS) = IFLD
    ENDDO
  !$OMP END PARALLEL DO
  #ifdef COMVERBOSE
    call MPI_BARRIER(MPI_COMM_WORLD,IERROR)
    Tc=(TIMEF()-Tc)/1000.0_JPRBT
    CALL MPI_COMM_RANK(MPI_COMM_WORLD, IRANK, IERROR)
    !IF(irank==0) WRITE(*,*) "packing (trgtol) in sec: ", Tc
  #endif
  
  CALL GSTATS(1602,1)
  
  IF(.NOT.LGPNORM)THEN
    CALL GSTATS(803,0)
  ELSE
    CALL GSTATS(804,0)
  ENDIF
  IR=0
  
  #ifdef COMVERBOSE
    call MPI_BARRIER(MPI_COMM_WORLD,IERROR)
    Tc=TIMEF()
  #endif
  !  Receive loop.........................................................
  DO INR=1,INRECV
    IR=IR+1
    IRECV=JRECV(INR)
    CALL MPL_RECV(ZCOMBUFR(-1:IRECVTOT(IRECV),INR), &
      & KSOURCE=NPRCIDS(IRECV), &
      & KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=IREQ(IR), &
      & KTAG=ITAG,CDSTRING='TRLTOG:' )
  !print*,irank,size(ZCOMBUFR(-1:IRECVTOT(IRECV),INR))
  ENDDO
  
  !....Send loop.........................................................
  DO INS=1,INSEND
    IR=IR+1
    ISEND=JSEND(INS)
    CALL MPL_SEND(ZCOMBUFS(-1:ISENDTOT(ISEND),INS),KDEST=NPRCIDS(ISEND), &
     & KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=IREQ(IR), &
     & KTAG=ITAG,CDSTRING='TRGTOL:' )
  ENDDO
  
  IF(IR > 0) THEN
    CALL MPL_WAIT(KREQUEST=IREQ(1:IR), &
      & CDSTRING='TRGTOL: WAIT FOR SENDS AND RECEIVES')
  ENDIF
  
  !#ifdef COMVERBOSE
  !  call MPI_BARRIER(MPI_COMM_WORLD,IERROR)
  !  Tc=(TIMEF()-Tc)/1000.0_JPRBT
  !  !IF(irank==0) WRITE(*,*) "non-CUDA-aware isend/irecv (trgtol) in sec: ", Tc
  !#endif
  
  IF(.NOT.LGPNORM)THEN
    CALL GSTATS(803,1)
  ELSE
    CALL GSTATS(804,1)
  ENDIF
  
  !#ifdef COMVERBOSE
  !  call MPI_BARRIER(MPI_COMM_WORLD,IERROR)
  !  Tc=TIMEF()
  !#endif
  !  Unpack loop.........................................................
  
  CALL GSTATS(1603,0)


  !$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JL,II,JFLD,INR,IRECV,ILEN,IRECV_FLD_START,IRECV_FLD_END,IPOS)
  DO INR=1,INRECV
      IRECV=JRECV(INR)
      ILEN = IRECVTOT(IRECV)/KF_FS
      IRECV_FLD_START = ZCOMBUFR(-1,INR)
      IRECV_FLD_END   = ZCOMBUFR(0,INR)
      DO JFLD=IRECV_FLD_START,IRECV_FLD_END
        DO JL=1,ILEN
          II = KINDEX(INDOFF(IRECV)+JL)-1
          PREEL_REAL(JFLD+KF_FS*II) = ZCOMBUFR(JL+(JFLD-IRECV_FLD_START)*ILEN,INR)
        ENDDO
      ENDDO
  ENDDO
  !$OMP END PARALLEL DO

  CALL GSTATS(1603,1)
  
  IF (IBUFLENS > 0) DEALLOCATE(ZCOMBUFS)
  IF (IBUFLENR > 0) DEALLOCATE(ZCOMBUFR)

  ! this is not efficient but I don't care about this part of code
  !$ACC UPDATE DEVICE(PREEL_REAL)

  !$ACC DATA PRESENT(PREEL_REAL) CREATE(PREEL_REAL_TMP)
  !$ACC KERNELS
  PREEL_REAL_TMP(:) = PREEL_REAL(:)
  !$ACC END KERNELS

  !$ACC PARALLEL LOOP COLLAPSE(2) DEFAULT(NONE)
  DO KGL=1,D%NDGL_FS
    DO KF=1,KF_FS
      !$ACC LOOP SEQ
      DO KI=1,D%NSTAGTF(KGL+1)-D%NSTAGTF(KGL)
        PREEL_REAL(KF_FS*D%NSTAGTF(KGL)+(KF-1)*(D%NSTAGTF(KGL+1)-D%NSTAGTF(KGL))+KI) = &
            & PREEL_REAL_TMP(KF_FS*D%NSTAGTF(KGL)+(KI-1)*KF_FS+KF)
      ENDDO
    ENDDO
  ENDDO
  !$ACC END DATA

  IF (LHOOK) CALL DR_HOOK('TRGTOL',1,ZHOOK_HANDLE)
  
  END SUBROUTINE TRGTOL
  END MODULE TRGTOL_MOD
